val Functions$Function3<SwitchItem, SwitchItem, StringItem, Void> handleShortPress =  [ SwitchItem lightBulbSwitch, SwitchItem toggleLightOnMotion, StringItem lightBulbAlert |
	logInfo("switches.rules", "handleShortPress()");
	
	if (lightBulbSwitch.state == OFF)
	{
		logInfo("switches.rules", "turning light ON...");
		sendCommand(lightBulbSwitch, ON);
		sendCommand(toggleLightOnMotion, OFF);
	}
	else
	{
		if (toggleLightOnMotion.state == OFF)
		{
			logInfo("switches.rules", "turning light OFF...");
			sendCommand(lightBulbSwitch, OFF);
		}
		else
		{
			logInfo("switches.rules", "disabling automatic mode...");
			sendCommand(toggleLightOnMotion, OFF);
			sendCommand(lightBulbAlert, "SELECT");
		}
	}
]

val Functions$Function4<SwitchItem, DimmerItem, StringItem, SwitchItem, Void> handleDoublePress =  [SwitchItem lightBulbSwitch, DimmerItem lightBulbDimmer, StringItem lightBulbAlert, SwitchItem toggleLightOnMotion |
	logInfo("switches.rules", "handleDoublePress()");

	var shouldDisableMotionControl = (lightBulbSwitch.state == OFF);
	var shouldSetMaxBrightness = (lightBulbSwitch.state == ON);
	
	if (shouldDisableMotionControl)
	{
		sendCommand(lightBulbAlert, "SELECT");
		sendCommand(toggleLightOnMotion, OFF);
	}
	
	if (shouldSetMaxBrightness)
	{
		lightBulbDimmer.sendCommand(100);
	}
]

val Functions$Function1<DimmerItem, Void> handleLongPress =  [DimmerItem lightBulbDimmer |
	logInfo("switches.rules", "handleLongPress()");

	var newBrightness = (lightBulbDimmer.state as DecimalType) + 20;
			
	if (newBrightness > 100) {
		newBrightness = 20;
	}
	
	lightBulbDimmer.sendCommand(newBrightness);
]



rule "Hue - Quarto do Pedro"
when
    Channel "mihome:sensor_switch:158d00019de580:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			logInfo("switches.rules", "Short pressed switch_quarto_pedro");
			
			handleShortPress.apply(hue_quarto_pedro_switch, EnableLightOnMotion_QuartoPedro, hue_quarto_pedro_alert);
			
			if (now.getHourOfDay > 1 && now.getHourOfDay < 8 && hue_quarto_pedro_switch.state == OFF)
			{
				sendCommand(EnableLightOnMotion_QuartoPedro, OFF);
				logInfo("switches.rules", "disabling light automatic mode, as it's night time.");
			}
        }
		
        case "DOUBLE_PRESSED": {
			logInfo("switches.rules", "Double pressed switch_quarto_pedro");
			handleDoublePress.apply(hue_quarto_pedro_switch, hue_quarto_pedro_brightness, hue_quarto_pedro_alert, EnableLightOnMotion_QuartoPedro);
        }
		
        case "LONG_PRESSED": {
			logInfo("switches.rules", "Long pressed switch_quarto_pedro");
			handleLongPress.apply(hue_quarto_pedro_brightness);
        }
		
        case "LONG_RELEASED": {
        }
    }
end


rule "Hue - Sala de Jantar"
when
    Channel "mihome:sensor_switch:158d00019dcdda:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
	switch(actionName) {
	
        case "SHORT_PRESSED": {
			logInfo("switches.rules", "Short pressed switch_sala_jantar");
			handleShortPress.apply(hue_sala_jantar_switch, EnableLightOnMotion_SalaDeJantar, hue_sala_jantar_alert);
        }
		
        case "DOUBLE_PRESSED": {
			logInfo("switches.rules", "Double pressed switch_sala_jantar");
			handleDoublePress.apply(hue_sala_jantar_switch, hue_sala_jantar_brightness, hue_sala_jantar_alert, EnableLightOnMotion_SalaDeJantar);
        }
		
        case "LONG_PRESSED": {
			logInfo("switches.rules", "Long pressed switch_sala_jantar");
			handleLongPress.apply(hue_sala_jantar_brightness);
        }
		
        case "LONG_RELEASED": {
        }
    }
end


rule "Hue - Sala de Estar"
when
    Channel "mihome:sensor_switch:158d00019dcc92:button" triggered
then
    var actionName = receivedEvent.getEvent()
	
    switch(actionName) {
	
        case "SHORT_PRESSED": {
			logInfo("switches.rules", "Short pressed switch_sala_estar");
			handleShortPress.apply(hue_sala_estar_switch, EnableLightOnMotion_SalaDeEstar, hue_sala_estar_alert);
        }
		
        case "DOUBLE_PRESSED": {
			logInfo("switches.rules", "Double pressed switch_sala_estar");
			handleDoublePress.apply(hue_sala_estar_switch, hue_sala_estar_brightness, hue_sala_estar_alert, EnableLightOnMotion_SalaDeEstar);
        }
		
        case "LONG_PRESSED": {
			logInfo("switches.rules", "Long pressed switch_sala_estar");
			handleLongPress.apply(hue_sala_estar_brightness);
        }
		
        case "LONG_RELEASED": {
        }
    }
end